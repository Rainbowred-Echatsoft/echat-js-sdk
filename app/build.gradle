apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
//        applicationId "com.github.echatmulti.sample"
        applicationId "com.echat.echatjsdemo"//申请推送SDK请更名这个id

        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "XIAOMI_PUSH_ID", "\"\""
        buildConfigField "String", "XIAOMI_PUSH_KEY", "\"\""
        buildConfigField "String", "UMENG_PUSH_APPKEY", "\"\""
        buildConfigField "String", "UMENG_MESSAGE_SECRET", "\"\""
        manifestPlaceholders = [app_name: "一洽商城", HUAWEI_PUSH: ""]
        buildConfigField "boolean", "dev", "false"

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    signingConfigs {
        /**
         * 设置你的keystore相关
         * demo中只是一个示例，使用时请根据实际情况进行配置
         */
        debug {
            //keystore中key的别名
            keyAlias 'echatdebug'
            //keystore中key的密码
            keyPassword 'echatsoft'
            //keystore的文件路径，可以是绝对路径也可以是相对路径
            storeFile file('debug.jks')
            //keystore的密码
            storePassword 'echatsoft'
        }
    }


    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "default"
    productFlavors {
        //多商户
        multi {
            applicationIdSuffix ".multi"
            manifestPlaceholders = [app_name             : "一洽多商户",
                                    HUAWEI_PUSH          : "appid=100740549",
                                    "APP_LOGO_PATH"      : "@mipmap/ic_echatmall_multi",
                                    "APP_LOGO_ROUND_PATH": "@mipmap/ic_echatmall_multi_round"]
            buildConfigField "String", "XIAOMI_PUSH_ID", '"2882303761517993713"'
            buildConfigField "String", "XIAOMI_PUSH_KEY", '"5461799342713"'
            buildConfigField "String", "UMENG_PUSH_APPKEY", '"5cc035db4ca357d6b80002bf"'
            buildConfigField "String", "UMENG_MESSAGE_SECRET", '"d316e8be9bac27bdb864cdf0941221a1"'
            buildConfigField "String", "HOME_URL", '"https://demo.echatsoft.com/web/html/platform/"'
            buildConfigField "boolean", "dev", "false"

            buildConfigField "String[]", "URL_ARRAY",
                    "{" +
                            "\"https://demo.echatsoft.com/app/\"," +
                            "\"https://demo.rainbowred.com/app/\"," +
                            "\"https://app.echatsoft.com/app/\"," +
                            "\"https://demo.echatsoft.com/web/html/platform/\"" +
                            "}"
        }

        //多商户开发
        multidev {
            applicationIdSuffix ".multi"
            manifestPlaceholders = [app_name             : "一洽多商户开发",
                                    HUAWEI_PUSH          : "appid=100740549",
                                    "APP_LOGO_PATH"      : "@mipmap/ic_echatmall_multi",
                                    "APP_LOGO_ROUND_PATH": "@mipmap/ic_echatmall_multi_round"
            ]
            buildConfigField "boolean", "dev", "true"
            buildConfigField "String", "XIAOMI_PUSH_ID", '"2882303761517993713"'
            buildConfigField "String", "XIAOMI_PUSH_KEY", '"5461799342713"'
            buildConfigField "String", "UMENG_PUSH_APPKEY", '"5cc035db4ca357d6b80002bf"'
            buildConfigField "String", "UMENG_MESSAGE_SECRET", '"d316e8be9bac27bdb864cdf0941221a1"'
            buildConfigField "String", "HOME_URL", '"https://app.echatsoft.com/web/html/platform/"'

            buildConfigField "String[]", "URL_ARRAY",
                    "{" +
                            "\"https://app.echatsoft.com/web/html/platform/\"," +
                            "\"https://demo.echatsoft.com/web/html/platform/\"" +
                            "}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "echatmall-${variant.productFlavors[0].name}-${versionName}-${buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-vector-drawable:28.0.0'


    implementation 'me.majiajie:pager-bottom-tab-strip:2.3.0'
    implementation 'com.gyf.immersionbar:immersionbar:2.3.3'

    //原生图片
    implementation('com.github.maning0303:MNImageBrowser:V1.2.0') {
        exclude group: 'com.android.support'
    }
    //图库
    implementation 'com.zhihu.android:matisse:0.5.2-beta4'
    // 微信拍照
    implementation 'com.echatsoft.echatsdk:cameralibrary:1.0.0-beta1'
    //PushSDK必须依赖基础组件库，所以需要加入对应依赖
    implementation 'com.umeng.umsdk:common:1.5.4'
    //PushSDK必须依赖utdid库，所以需要加入对应依赖
    implementation 'com.umeng.umsdk:utdid:1.1.5.3'
    //PushSDK
    implementation 'com.umeng.umsdk:push:5.0.2'
    //小米
    implementation 'com.umeng.umsdk:xiaomi-push:3.6.9'
    implementation 'com.umeng.umsdk:xiaomi-umengaccs:1.0.9'
    //华为Push通道
    implementation 'com.umeng.umsdk:huawei-basetb:2.6.0.301'
    implementation 'com.umeng.umsdk:huawei-pushtb:2.6.0.301'
    implementation 'com.umeng.umsdk:huawei-umengaccs:1.1.3'


    //视频解码播放
    //implementation 'cn.jzvd:jiaozivideoplayer:6.4.3'
    implementation 'com.echatsoft.echatsdk:videoplayer:1.0.0-beta2'
    implementation 'tv.danmaku.ijk.media:ijkplayer-java:0.8.4'
    implementation 'tv.danmaku.ijk.media:ijkplayer-armv7a:0.8.4'
    implementation 'tv.danmaku.ijk.media:ijkplayer-armv5:0.8.4'
    implementation 'tv.danmaku.ijk.media:ijkplayer-x86:0.8.4'
    implementation 'tv.danmaku.ijk.media:ijkplayer-arm64:0.8.4'
    implementation 'com.blankj:utilcode:1.23.7'
    implementation 'org.greenrobot:eventbus:3.1.1'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
