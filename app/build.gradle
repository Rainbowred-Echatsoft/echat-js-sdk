apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
//        applicationId "com.github.echatmulti.sample"
        applicationId "com.echat.echatjsdemo"//申请推送SDK请更名这个id

        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0.0.12"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "XIAOMI_PUSH_ID", "\"\""
        buildConfigField "String", "XIAOMI_PUSH_KEY", "\"\""
        buildConfigField "String", "UMENG_PUSH_APPKEY", "\"\""
        buildConfigField "String", "UMENG_MESSAGE_SECRET", "\"\""
        manifestPlaceholders = [app_name: "一洽商城", HUAWEI_PUSH: ""]
        buildConfigField "boolean", "dev", "false"

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    signingConfigs {
        /**
         * 设置你的keystore相关
         * demo中只是一个示例，使用时请根据实际情况进行配置
         */
        debug {
            //keystore中key的别名
            keyAlias 'echatdebug'
            //keystore中key的密码
            keyPassword 'echatsoft'
            //keystore的文件路径，可以是绝对路径也可以是相对路径
            storeFile file('debug.jks')
            //keystore的密码
            storePassword 'echatsoft'
        }
    }


    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "default"
    productFlavors {

        //单商户
        single {
            applicationIdSuffix ".single"
            manifestPlaceholders = [app_name             : "一洽商城",
                                    HUAWEI_PUSH          : "appid=100912273",
                                    "APP_LOGO_PATH"      : "@mipmap/ic_echatmall_multi",
                                    "APP_LOGO_ROUND_PATH": "@mipmap/ic_echatmall_multi_round"]
            buildConfigField "boolean", "dev", "true"
            buildConfigField "String", "XIAOMI_PUSH_ID", '"2882303761518049660"'
            buildConfigField "String", "XIAOMI_PUSH_KEY", '"5791804946660"'
            buildConfigField "String", "UMENG_PUSH_APPKEY", '"5d1f27930cafb27a4c000298"'
            buildConfigField "String", "UMENG_MESSAGE_SECRET", '"903e038d4c2655ff4b517c42e3370439"'
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "echatmall-${variant.productFlavors[0].name}-${versionName}-${buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-vector-drawable:28.0.0'


//    implementation 'com.echatsoft.jsapp:chatlib:1.0.2-professional'
    implementation project(':chatlib')


    implementation 'me.majiajie:pager-bottom-tab-strip:2.3.0'
    implementation 'com.gyf.immersionbar:immersionbar:2.3.3'


    // 二维码扫描
    implementation 'cn.yipianfengye.android:zxing-library:2.2'

    // 网络请求库
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'

    //PushSDK必须依赖基础组件库，所以需要加入对应依赖
    implementation 'com.umeng.umsdk:common:2.1.0'
    //PushSDK必须依赖utdid库，所以需要加入对应依赖
    implementation 'com.umeng.umsdk:utdid:1.1.5.3'
    //PushSDK
    implementation 'com.umeng.umsdk:push:6.0.1'
    //小米
    implementation 'com.umeng.umsdk:xiaomi-push:3.6.17'
    implementation 'com.umeng.umsdk:xiaomi-umengaccs:1.1.0'
    //华为Push通道
    implementation 'com.umeng.umsdk:huawei-basetb:2.6.3.305'
    implementation 'com.umeng.umsdk:huawei-pushtb:2.6.3.305'
    implementation 'com.umeng.umsdk:huawei-umengaccs:1.2.1'

    //视频解码播放
    //implementation 'cn.jzvd:jiaozivideoplayer:6.4.3'

    implementation 'com.blankj:utilcode:1.25.7'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
}
